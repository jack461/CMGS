
@init

/********************************************************/
function _butbr_compute_coeffs()
(
    bw_ = this.freq / this.q;
    c_ = tan(this.pioversr * bw_);
    d_ = 2.0 * cos(2.0 * this.pioversr * this.freq);
    this.a0 = this.a2 = 1.0 / (1.0 + c_);
    this.a1 = this.b1 = -this.a0 * d_;
    this.b2 = this.a0 * (1.0 - c_);
);

function butbr(freq q)
(
    freq < 1.0 ? freq = 1.0 : freq > this.ny ? freq = this.ny;
    this.freq = freq;
    q < 0.5 ? q = 0.5 : q > 1000 ? q = 1000;
    this.q = q;
    this.x1 = this.x2 = this.y1 = this.y2 = 0.0;
    this.pioversr = $pi / srate;
    this.ny = srate * 0.49;
    this._butbr_compute_coeffs();
);


function butbr_set_freq(freq)
(
    freq < 1.0 ? freq = 1.0 : freq > this.ny ? freq = this.ny;
    this.freq = freq;
    this._butbr_compute_coeffs();
);

function butbr_set_q(q)
(
    q < 0.5 ? q = 0.5 : q > 1000 ? q = 1000;
    this.q = q;
    this._butbr_compute_coeffs();
);

function butbr_do(signal)
(
    tmp_ = this.a0 * signal + this.a1 * this.x1 + this.a2 * this.x2 - this.b1 * this.y1 - this.b2 * this.y2;
    this.x2 = this.x1; this.x1 = signal; this.y2 = this.y1; this.y1 = tmp_;
    tmp_;
);

