///-------------------------------------///
///----------  Menu Utilities  ---------///
///--------------- Jack461 -------------///
@init
/*
   A menu is described by an array of values
   Entries in this array are indexed by the following constants
   A code example is provided somewhere...


    1: left mouse button
    2: right mouse button
    4: Control key (Windows), Command key (OSX)
    8: Shift key
    16: Alt key (Windows), Option key (OSX)
    32: Windows key (Windows), Control key (OSX)
    64: middle mouse button

*/
ttt = 0;
// Menu description
ui_jbtmask = 0x3f; // mask for the block type
ui_jmouse_modifiers = 0x3C;
ui_jmouse_bmask = 0x43;
ui_jmenu_shift = 4;
ui_jmenu_tickMask = (1 << ui_jmenu_shift) - 1;
ui_jstring_separator = '|'; // default string separator
ui_jmenu_max_items = 256; // choose max number of items in a menu
ui_jmenu_strSP = "-";
ui_jmenu_noSel = '/';
ui_jmenu_noDisp = '~';
ui_jmenu_chSP = str_getchar(ui_jmenu_strSP, 0, 'cu');
ui_jmenu_prefx = 6; // size of the menu prefix
ui_jmenu_provided_block = 0; // memory block for menu
ui_jmenu_type = 0; // 0 : index of menu type
ui_jmenu_bsize = ui_jdx = 1; // 1 : index of block menu size
ui_jmenu_res = (ui_jdx += 1) ; // interaction result
ui_jmenu_rstring = (ui_jdx += 1) ; // interaction string index result
ui_jmenu_mstat = (ui_jdx += 1); // mouse & modifiers status
ui_jmenu_msmsk = (ui_jdx += 1); // mouse & modifiers mask
ui_jmenu_posx = (ui_jdx += 1); // x mouse position
ui_jmenu_posy = (ui_jdx += 1); // y mouse position
ui_jmenu_changed = (ui_jdx += 1); // menu changed flag
ui_jmenu_itc = (ui_jdx += 1); // items count
ui_jmenu_entitm = (ui_jdx += 1); // result of ui_menu_ent
ui_jmenu_ltick = (ui_jdx += 1); // last ticked string
ui_jmenu_titlsiz = (ui_jdx += 1); // menu title size, a number
// all entries after this comment are string numbers
ui_jmenu_str = (ui_jdx += 1); // menu working string
ui_jmenu_entstr = (ui_jdx += 1); // result of ui_menu_ent, a string
ui_jmenu_titlfnt = (ui_jdx += 1); // menu title font, a string
ui_jmenu_selection = (ui_jdx += 1); // last menu selection, a string
ui_jmenu_title = (ui_jdx += 1); // menu title, a string - keep just before items
ui_jmenu_items = (ui_jdx += 1); // first item and following

// Menu type values and flags
//    entry ui_jmenu_type is a combination of the following constants
//    exclusive :
ui_jmenu_typ_action = 1; // type "action"
ui_jmenu_typ_sel = 2; // type "selection"
ui_jmenu_typ_opt = 3; // type "options"
ui_jmenu_typ_none = 4; // type "unspecified"
//    options :
ui_jmenu_opt_rect       =        0x40; // draw a rectangular border
ui_jmenu_opt_rndrect    =        0x80; // draw a rounded rectangle
ui_jmenu_opt_usesel     =       0x100; // use last selected item as menu title
ui_jmenu_opt_montop     =       0x200; // draw menu on top of title
ui_jmenu_opt_notitle    =       0x400; // do not display title
ui_jmenu_opt_dynalloc   =       0x800; // dynamically alloc rather than static
ui_jmenu_opt_lclick     =      0x1000; // accept left click
ui_jmenu_opt_rclick     =      0x2000; // accept right click
ui_jmenu_opt_alclick    =      0x4000; // accept "alt" left click
ui_jmenu_opt_arclick    =      0x8000; // accept "alt" right click
ui_jmenu_opt_shlclick   =     0x10000; // accept "shift" left click
ui_jmenu_opt_salclick   =     0x14000; // accept "shift" or "alt" left click
ui_jmenu_opt_s_alclick  =     0x16000; // accept "shift" + "alt" left click
ui_jmenu_opt_shrclick   =     0x20000; // accept "shift" right click
ui_jmenu_opt_sarclick   =     0x28000; // accept "shift" or "alt" right click
ui_jmenu_opt_s_arclick  =     0x29000; // accept "shift" + "alt" right click
ui_jmenu_opt_kmlclick   =     0x40000; // accept "cmd" left click
ui_jmenu_opt_aklclick   =     0x44000; // accept "alt" or "cmd" left click
ui_jmenu_opt_a_klclick  =     0x46000; // accept "alt" + "cmd" left click
ui_jmenu_opt_kmrclick   =     0x80000; // accept "cmd" right click
ui_jmenu_opt_akrclick   =     0x88000; // accept "alt" or "cmd" right click
ui_jmenu_opt_akrclick   =     0x89000; // accept "alt" + "cmd" right click
ui_jmenu_opt_ctlclick   =    0x100000; // accept "ctrl" left click
ui_jmenu_opt_ctrclick   =    0x200000; // accept "ctrl" right click
ui_jmenu_opt_cclick     =    0x400000; // accept center click
ui_jmenu_opt_kmcclick   =    0x800000; // accept "cmd" center click
ui_jmenu_opt_alcclick   =   0x1000000; // accept "alt" center click
ui_jmenu_opt_shcclick   =   0x2000000; // accept "shift" center click
ui_jmenu_opt_ctcclick   =   0x4000000; // accept "ctrl" center click
ui_jmenu_opt_all_clicks =  0x1ffff000;
ui_jmenu_opt_mask       =  0x7ffff000; // mask for tests




/*
    For an ent "n", return
    the actual item number, and
    a copy or the string item in menu[ui_jmenu_entstr]
*/
function ui_menu_ent(menu, ent)
local(itc, stn, i, s)
(
    stn = i = 0;
    ((menu[-2] === 'Menu') && ((s = menu[-1]) === menu[s])) ? ( // quick check menu
        itc = menu[ui_jmenu_itc];
        (ent >= 1 && ent <= itc) ? (
            while (ent > i) (
                stn += 1;
                (str_getchar(menu[ui_jmenu_items - 1 + stn], 0, 'cu') === '*') ? (i += 1;);
            );
            menu[ui_jmenu_entstr] = menu[ui_jmenu_items - 1 + stn];
        );
        menu[ui_jmenu_entitm] = stn;
    );
    stn;
);



// Set a tick mark for entry "ent" of "menu"
//     tick == 0 => reset tick
//     tick == 1 => set tick
//     tick == -1 => invert tick
function ui_menu_tick(menu, ent, tick)
local (k, itc, ty, stn, stp, prev, i, p, s, mod, tcl, ch)
(
    k = 0;
    ((menu[-2] === 'Menu') && ((s = menu[-1]) === menu[s])) ? ( // quick check of menu
        itc = menu[ui_jmenu_itc];
        mod = 0;
        ty = menu[ui_jmenu_type] & ui_jbtmask; // the menu type
        (ent >=1 && ent <= itc) ? (
            // Get actual string entry
            stn = i = 0;
            while ((ent > i) && (stn < itc)) (
                stn += 1;
                (str_getchar(menu[ui_jmenu_items -1 + stn], 0, 'cu') === '*') ? (i += 1;);
            );
            // stn is the string entry corresponding to "ent" choice [1st is 1]
            stp = menu[ui_jmenu_items -1 + stn]; // stp is its string pointer
            prev = str_getchar(stp, 1, 'cu'); // get previous status
            tcl = str_getchar(stp, 2, 'cu'); // get tick class
            ((ty === ui_jmenu_typ_sel) || (ty === ui_jmenu_typ_action))  ? (
                i = 0; // reset all entries
                loop(itc,
                    p = menu[ui_jmenu_items+i];
                    ch = str_getchar(p, 2, 'cu');
                    // (str_getchar(p, 1, 'cu') === '!') ? (
                    ((ch === ui_jmenu_chSP) || (ch === tcl) || (ch === ui_jmenu_noSel)) ? (
                        str_setchar(p, 1, ui_jmenu_chSP);
                        mod |= 1;
                    );
                    i += 1;
                );
            ) :
            (ty === ui_jmenu_typ_opt) ? (
                i = 0; // reset all entries
                loop(itc,
                    p = menu[ui_jmenu_items+i];
                    ch = str_getchar(p, 2, 'cu');
                    // (str_getchar(p, 1, 'cu') === '!') ? (
                    (ch === ui_jmenu_noSel) ? (
                        str_setchar(p, 1, ui_jmenu_chSP);
                        mod |= 1;
                    );
                    i += 1;
                );
            );

            (ty === ui_jmenu_typ_sel) ? (
                // Selection : only one is ticked
                (str_getchar(stp, 2, 'cu') != ui_jmenu_noSel) ? str_setchar(stp, 1, '!');
                mod |= 1;
            ) :
            (ty === ui_jmenu_typ_opt) ? (
                (tick === 0) ? (
                    // reset string tick
                    (str_getchar(stp, 1, 'cu') === '!') ? (
                        str_setchar(stp, 1, ui_jmenu_chSP);
                        mod |= 1;
                    );
                ) :
                (tick === 1) ? (
                    // set string tick
                    ((str_getchar(stp, 1, 'cu') !== '!') && (str_getchar(stp, 2, 'cu') !== ui_jmenu_noSel)) ? (
                        str_setchar(stp, 1, '!');
                        mod |= 1;
                    );
                ) :
                (tick === -1) ? (
                    str_setchar(stp, 1, ((prev === '!') || (str_getchar(stp, 2, 'cu') === ui_jmenu_noSel)) ? ui_jmenu_chSP : '!');
                    mod |= 1;
                );
            );
            // note : for "actions menus", tick marks are not allowed
            strcpy_from(menu[ui_jmenu_selection], stp, ui_jmenu_prefx);
            menu[ui_jmenu_rstring] = stn;
            menu[ui_jmenu_changed] = mod;
            menu[ui_jmenu_res] = ent;
            menu[ui_jmenu_ltick] = stn;
            k = str_getchar(stp, 1, 'cu') === '!';
            s = #; strcpy_from(s,menu[ui_jmenu_items -1 + stn], ui_jmenu_prefx);
            ttt ? sprintf(#dbg_trace2, "T:%d [%d %d %d] %d m:%s/%s", k, itc, ent, stn, mod, menu[ui_jmenu_title], s);
        );
    );
    k;
);


// Get the tick mark for entry "ent" of "menu"
//    returns 1 if the tick is set, 0 otherwise
function ui_menu_tick(menu, ent)
local (k, s, itc, ent, stn, i)
(
    k = 0;
    ((menu[-2] === 'Menu') && ((s = menu[-1]) === menu[s])) ? ( // quick check of menu
        itc = menu[ui_jmenu_itc];
        (ent >=1 && ent <= itc) ? (
            // Get actual string entry
            stn = i = 0;
            while (ent > i) (
                stn += 1;
                (str_getchar(menu[ui_jmenu_items -1 + stn], 0, 'cu') === '*') ? (i += 1;);
            );
            // stn is the string entry corresponding to "ent" choice
            k = (str_getchar(menu[ui_jmenu_items -1 + stn], 1, 'cu') === '!');
        );
    );
    k;
);


function ui_menu_item(menu, ent)
local (k, s, itc, ent, stn, i)
(
    k = #; strcpy(k, "");
    ((menu[-2] === 'Menu') && ((s = menu[-1]) === menu[s])) ? ( // quick check of menu
        itc = menu[ui_jmenu_itc];
        (ent >=1 && ent <= itc) ? (
            // Get actual string entry
            stn = i = 0;
            while (ent > i) (
                stn += 1;
                (str_getchar(menu[ui_jmenu_items -1 + stn], 0, 'cu') === '*') ? (i += 1;);
            );
            // stn is the string entry corresponding to "ent" choice
            strcpy_from(k,menu[ui_jmenu_items -1 + stn], ui_jmenu_prefx);
        );
    );
    k;
);


// ent is an actual item number [0 to N-1] in the menu source
// return the corresponding result of control_jmenu(menu) [1 to N]
function jmenu_item2res(menu, ent)
local (k, s, itc, itn, rnb)
(
    k = 0;
    ((menu[-2] === 'Menu') && ((s = menu[-1]) === menu[s])) ? ( // quick check of menu
        itc = menu[ui_jmenu_itc]; // item count
        itn = 0; rnb = 0;
        while ((ent >= itn) && (itn < itc)) (
            k = rnb;
            (str_getchar(menu[ui_jmenu_items + itn], 0, 'cu') === '*') ? (rnb += 1;);
            itn += 1;
        );
        k += 1;
    );
    k;
);


// res is the result of control_jmenu(menu) [1 to N]
// return the actual item number in the menu source [0 to N-1]
function jmenu_res2item(menu, res)
local (k, s, itc, itn, rnb)
(
    k = 0;
    ((menu[-2] === 'Menu') && ((s = menu[-1]) === menu[s])) ? ( // quick check of menu
        itc = menu[ui_jmenu_itc]; // item count
        itn = 0; rnb = 0;
        while ((res > rnb) && (itn < itc)) (
            k = itn;
            (str_getchar(menu[ui_jmenu_items + itn], 0, 'cu') === '*') ? (rnb += 1;);
            itn += 1;
        );
    );
    k;
);


// Draw and Control a menu
// result is :
//    0 : no menu item was selected
//    n>0 : item "n" of menu was selected
//
function control_jmenu(menu)
local (z, ty, mty, kmc, f, fl, rs, s, cnt, mstr, idx, sst, tmp, tt)
(
    z = 0; tt = #;
    ((menu[-2] === 'Menu') && ((s = menu[-1]) === menu[s])) ? ( // quick check of menu
        ty = menu[ui_jmenu_type]; // the menu type
        mty = ty & ui_jmenu_opt_mask;
        ui_jmouse_cap = mouse_cap; // need value before calling click()
        rs = uix_drawconfig_current[9]; // retina scaling
        kmc = ui_jmouse_cap & ui_jmouse_modifiers & menu[ui_jmenu_msmsk]; // key modifiers on click
            ttt ?     sprintf(#dbg_trace, "Enter %d: %x %x %x %x", menu, ty, mty, kmc, ui_jmouse_cap);
        !(ty & ui_jmenu_opt_notitle) ? (
            (ty & ui_jmenu_opt_rect) ? ( ui_border(); );
            (ty & ui_jmenu_opt_rndrect) ? ( gfx_roundrect(ui_left_retina(), ui_top_retina(), ui_width_retina()-1, ui_height_retina()-1, 5); );
            ((s = menu[ui_jmenu_titlsiz]) != 0) ? (
                ui_fontface(menu[ui_jmenu_titlfnt]);
                ui_fontsize(s);
            );
            ui_text((ty & ui_jmenu_opt_usesel) ? (
                strcpy_from(tt, menu[ui_jmenu_selection], ui_jmenu_prefx); tt
            ) : (
                menu[ui_jmenu_title]
            ));
        );

        (
            (ui_click(1) && (   // left click
                ((mty === ui_jmenu_opt_lclick) && (kmc === 0))         // no modifiers
                ||  ((mty === ui_jmenu_opt_kmlclick) && (kmc === 4))   // kmd key
                ||  ((mty === ui_jmenu_opt_shlclick) && (kmc === 8))   // shift key
                ||  ((mty === ui_jmenu_opt_alclick) && (kmc === 16))   // alt key
                ||  ((mty === ui_jmenu_opt_ctlclick) && (kmc === 32))  // ctrl key
                ||  ((mty === ui_jmenu_opt_aklclick) && ((kmc === 4) || (kmc === 16)))  // kmd/alt key
                ||  ((mty === ui_jmenu_opt_a_klclick) && (kmc === 20))  // kmd+alt key
                ||  ((mty === ui_jmenu_opt_salclick) && ((kmc === 8) || (kmc === 16)))  // shift/alt key
                ||  ((mty === ui_jmenu_opt_s_alclick) && (kmc === 24))  // shift+alt key
                )
            ) ||
            (ui_click(2) && (   // right click
                ((mty === ui_jmenu_opt_rclick) && (kmc === 0))         // no modifiers
                ||  ((mty === ui_jmenu_opt_kmrclick) && (kmc === 4))   // kmd key
                ||  ((mty === ui_jmenu_opt_shrclick) && (kmc === 8))   // shift key
                ||  ((mty === ui_jmenu_opt_arclick) && (kmc === 16))   // alt key
                ||  ((mty === ui_jmenu_opt_ctrclick) && (kmc === 32))  // ctrl key
                ||  ((mty === ui_jmenu_opt_akrclick) && ((kmc === 4) || (kmc === 16)))  // kmd/alt key
                ||  ((mty === ui_jmenu_opt_a_krclick) && (kmc === 20))  // kmd+alt key
                ||  ((mty === ui_jmenu_opt_sarclick) && ((kmc === 8) || (kmc === 16)))  // shift/alt key
                ||  ((mty === ui_jmenu_opt_s_arclick) && (kmc === 24))  // shift+alt key
                )
            ) ||
            (ui_click(64) && (  // center click
                ((mty === ui_jmenu_opt_cclick) && (kmc === 0))         // no modifiers
                ||  ((mty === ui_jmenu_opt_kmcclick) && (kmc === 4))   // kmd key
                ||  ((mty === ui_jmenu_opt_shcclick) && (kmc === 8))   // shift key
                ||  ((mty === ui_jmenu_opt_alcclick) && (kmc === 16))   // alt key
                ||  ((mty === ui_jmenu_opt_ctcclick) && (kmc === 32))   // ctrl key
                )
            )
        ) ? (

            //(menu === menu_Stud_Mod) ? (
                ttt ?     sprintf(#dbg_trace1, "Menu %d: has been activated", menu);
            // );
            // #dbg_desc = "In the menu!";
            // #dbg_trace = "Also there";
            ttt ? sprintf(#dbg_trace,"M:%d %s ", menu, menu[ui_jmenu_title]);
            // strcpy(#dbg_trace, menu[ui_jmenu_items]);

            menu[ui_jmenu_posx] = uix_mouse_x * rs; // keep X and Y position of mouse at click
            menu[ui_jmenu_posy] = uix_mouse_y * rs;
           // (menu[ui_jmenu_changed]) ? (
                // recompute the string menu
                tmp = #;
                mstr = menu[ui_jmenu_str];
                strcpy(mstr, "");
                cnt = menu[ui_jmenu_itc];
                idx = fl = 0;
                while (cnt > 0) (
                    sst = menu[ui_jmenu_items+idx];
                    (str_getchar(sst, 2, 'cu') !== ui_jmenu_noDisp) ? (
                        (fl) ? strcat(mstr, "|");
                        (str_getchar(sst, 1, 'cu') === '!') ? strcat(mstr, "!");
                        (str_getchar(sst, 3, 'cu') === '#') ? strcat(mstr, "#");
                        (str_getchar(sst, 4, 'cu') === '>') ? strcat(mstr, ">");
                        (str_getchar(sst, 5, 'cu') === '<') ? strcat(mstr, "<");
                        strcpy_from(tmp,sst, ui_jmenu_prefx);
                        strcat(mstr, tmp);
                        fl = 1;
                    );
                    cnt -= 1;
                    idx += 1;
                );
                menu[ui_jmenu_changed] = 0;
            //);
            (ty & ui_jmenu_opt_notitle) ? (
                gfx_x = max(0, uix_mouse_x * rs - 20);
                gfx_y = max(0, uix_mouse_y * rs - 20);
            ) : (
                gfx_x = ui_left_retina();
                gfx_y = (ty & ui_jmenu_opt_montop) ? ui_top_retina() : ui_bottom_retina();
            ) ;
            z = gfx_showmenu(menu[ui_jmenu_str]);
            ttt ? sprintf(#dbg_trace1,"S:%s ", menu[ui_jmenu_str]);
            (z != 0) ? (
                ui_menu_tick(menu, z, -1); // also sets the ui_jmenu_res entry
                menu[ui_jmenu_mstat] = ui_jmouse_cap; // keep mouse cap status
                z; // result return
            );


            ttt ? sprintf(#dbg_trace7, "C4:\"%s\"", menu[ui_jmenu_items+4]);
            ttt ? sprintf(#dbg_trace6, "C3:\"%s\"", menu[ui_jmenu_items+3]);
            ttt ? sprintf(#dbg_trace5, "C2:\"%s\"", menu[ui_jmenu_items+2]);
            ttt ? sprintf(#dbg_trace4, "C1:\"%s\"", menu[ui_jmenu_items+1]);
            ttt ? sprintf(#dbg_trace3, "C0:\"%s\"", menu[ui_jmenu_items+0]);
        ) : (
            //(menu === menu_Stud_Mod) ? (
                ttt ? sprintf(#dbg_trace1, "Menu %d: has not been activated", menu);
            //);
        );

    );
    z;
);



// Control a menu
// use this function to associate a JSFX slider to the menu
// items numbering start at 1, so, for 10 items, use :
//    slider3:1<1,10,1>-my menu
// result is :
//    0 : no menu item was selected
//    n>0 : item "n" of menu was selected
//
function control_jmenu(menu, slidervar*)
local (z)
(
    z = control_jmenu(menu);
    menu[ui_jmenu_prevsl] != slidervar ? (
        z = slidervar;
        ui_menu_tick(menu, z, -1);
        menu[ui_jmenu_prevsl] = z;
   );
   (z != 0) ? ui_automate(slidervar, z);
   z;
);


/*
     Build a menu as an array
     "type" is the menu type with its flags
     "mName" is the menu name
     "mItems" are the menu items, separated by '|' character

     ex:
         my_menu = make_jmenu(ui_jmenu_typ_sel, "Title", "item1|item2|item3");
*/
function make_jmenu(type, mName, mItems)
local (itc, menu, w, str, ms, tab, sep, slg, cp, it, is, dst, iq, flgs, r, ch, scl)
(
    w = ui_jmenu_items + (itc = 1+ str_ch_count(mItems, ui_jstring_separator)); // compute item count in menu
    //menu = (type & ui_jmenu_opt_dynalloc) ? (
    //    mSL_Dyn_Alloc(w, 'Menu', mSL_StM_FlClear);
    //) : (
    //    mSL_StM_Alloc(w, 'Menu', mSL_StM_FlClear);
    //);
    ((ui_jmenu_provided_block > 0) && mSL_bl_check(ui_jmenu_provided_block)
            && (ui_jmenu_provided_block[-1] >= w)) ? (
        menu = ui_jmenu_provided_block; ui_jmenu_provided_block = 0;
    ) : (
        menu = mSL_StM_Alloc(w, 'Menu', mSL_StM_FlClear);
    );
    menu[ui_jmenu_bsize] = w; // total size of menu
    ((type & ui_jbtmask) === 0) ? (type |= ui_jmenu_typ_none; ); // choose an arbitrary type if none specified
    (type & ui_jmenu_opt_all_clicks) === 0 ? (
         // mouse button not specified; use left as default
         type |= ui_jmenu_opt_lclick;
    );
    menu[ui_jmenu_type] = type;
    menu[ui_jmenu_itc] = itc;
    menu[ui_jmenu_titlsiz] = 12; // title font size
    menu[ui_jmenu_msmsk] = 0x3C; // mouse & modifiers default mask
    menu[ui_jmenu_entstr] = 0; // result of "inq ent" fonction
    menu[ui_jmenu_titlfnt] = ms = mSL_Str_Perm(); strcpy(ms, "Lucida Grande"); // title font
    menu[ui_jmenu_title] = ms = mSL_Str_Perm(); strcpy(ms, mName); // menu title
    menu[ui_jmenu_selection] = ms = mSL_Str_Perm(); strcpy(ms, mName); // for last menu selection
    menu[ui_jmenu_str] = str = mSL_Str_Perm(); // allocate a mutable string to reconstruct this menu
    // in line
    tab = menu+ui_jmenu_items; sep = '|';
    strcpy(str, mItems); strcat(str, "|");
    slg = strlen(str);
    cp = it = is = 0;
    // work on each substring
    while ((is<slg) && (it<itc)) (
        // allocate an entry
        tab[it] = dst = mSL_Str_Perm(); // this will be a "permanent" string
        r = 1; flgs = 0; scl = ui_jmenu_chSP;
        while (r && (cp < slg)) (
            ch = str_getchar(str,cp, 'cu');
            (ch <= 0x20) ? ( cp += 1; ) :
            (ch === '!') ? ( flgs |= 1;  cp += 1; ) :
            (ch === '#') ? ( flgs |= 2;  cp += 1; ) :
            (ch === '>') ? ( flgs |= 4;  cp += 1; ) :
            (ch === '<') ? ( flgs |= 8;  cp += 1; ) :
            (ch === '&') ? (
                scl = str_getchar(str,cp+1, 'cu');
                (scl === 0) ? (scl = '+';);
                (scl === ui_jmenu_noDisp) ? ( flgs |= 0x20; );
                cp += 2; ) :
            (ch === '|') ? ( flgs |= 0x10;  r = 0; ) :
            (r = 0; );
        );
        strcpy(dst, ""); // init
        strcpy(#Awst, " "); str_setchar(#Awst, 0, scl, 'cu');
        strcat(dst, ((flgs & 0x34) === 0) ? "*" : ui_jmenu_strSP);
        strcat(dst, ((flgs & 0x11) === 1) ? "!" : ui_jmenu_strSP);
        strcat(dst, #Awst);
        strcat(dst, ((flgs & 0x12) === 2) ? "#" : ui_jmenu_strSP);
        strcat(dst, ((flgs & 0x14) === 4) ? ">" : ui_jmenu_strSP);
        strcat(dst, ((flgs & 0x18) === 8) ? "<" : ui_jmenu_strSP);
        is = cp;
        (!(flgs & 0x10)) ? (
            while (str_getchar(str,is, 'cu') != sep) (is += 1;);
            iq = is;
            while (str_getchar(str,iq - 1, 'cu') <= 0x20) (iq -= 1);
            strcpy_substr(#Awst,str,cp,iq-cp);
            strcat(dst, #Awst);
        );
        cp = is + 1;
        it += 1;
    );
    menu[ui_jmenu_changed] = 1;
    //sprintf(#dbg_trace7, "I4:\"%s\"", menu[ui_jmenu_items+4]);
    //sprintf(#dbg_trace6, "I3:\"%s\"", menu[ui_jmenu_items+3]);
    //sprintf(#dbg_trace5, "I2:\"%s\"", menu[ui_jmenu_items+2]);
    //sprintf(#dbg_trace4, "I1:\"%s\"", menu[ui_jmenu_items+1]);
    //sprintf(#dbg_trace3, "I0:\"%s\"", menu[ui_jmenu_items+0]);
    menu; // return the menu itself
);



/*
    Make a menu from an array of items
*/
function make_jmenu(type, mName, cItems, aItems)
local (itc, menu, w, idx, dst, src, ms, r, flgs, cp, ch, slg, is)
(
    w = ui_jmenu_items + (itc = 0|min(ui_jmenu_max_items, max(1, cItems)));
    ((ui_jmenu_provided_block > 0) && mSL_bl_check(ui_jmenu_provided_block)
            && (ui_jmenu_provided_block[-1] >= w)) ? (
        menu = ui_jmenu_provided_block; ui_jmenu_provided_block = 0;
    ) : (
        menu = mSL_StM_Alloc(w, 'Menu', mSL_StM_FlClear);
    );
    menu[ui_jmenu_bsize] = w;
    ((type & ui_jbtmask) === 0) ? (type |= ui_jmenu_typ_none; ); // choose an arbitrary type if none specified
    (type & ui_jmenu_opt_all_clicks) === 0 ? (
         // mouse button not specified; use left as default
         type |= ui_jmenu_opt_lclick;
    );
    menu[ui_jmenu_type] = type;
    menu[ui_jmenu_itc] = itc; // cItems;
    menu[ui_jmenu_titlsiz] = 12; // menu font size
    menu[ui_jmenu_msmsk] = 0x3C; // mouse & modifiers default mask
    menu[ui_jmenu_entstr] = 0; // result of "inq ent" fonction
    menu[ui_jmenu_titlfnt] = ms = mSL_Str_Perm(); strcpy(ms, "Lucida Grande"); // menu font
    menu[ui_jmenu_title] = ms = mSL_Str_Perm(); strcpy(ms, mName); // menu name
    menu[ui_jmenu_selection] = mSL_Str_Perm(); strcpy(ms, mName); // for last menu selection
    menu[ui_jmenu_str] = mSL_Str_Perm(); // allocate a mutable string to reconstruct this menu

    idx=0; loop(itc,
        menu[ui_jmenu_items+idx] = dst = mSL_Str_Perm();
        src = aItems[idx]; slg = strlen(src);
        r = 1; flgs = 0; scl = ui_jmenu_chSP; cp = 0;
        while (r && (cp < slg)) (
            ch = str_getchar(src,cp, 'cu');
            (ch <= 0x20) ? ( cp += 1; ) :
            (ch === '!') ? ( flgs |= 1;  cp += 1; ) :
            (ch === '#') ? ( flgs |= 2;  cp += 1; ) :
            (ch === '>') ? ( flgs |= 4;  cp += 1; ) :
            (ch === '<') ? ( flgs |= 8;  cp += 1; ) :
            (ch === '&') ? (
                scl = str_getchar(src,cp+1, 'cu');
                (scl === 0) ? (scl = '+';);
                (scl === ui_jmenu_noDisp) ? ( flgs |= 0x20; );
                cp += 2; ) :
            (r = 0; );
        );
        (cp === slg) ? (
            flgs |= 0x10;
        ) : (
            is = slg;
            while (r && (is > cp)) (
                (str_getchar(src, is - 1, 'cu') <= 0x20) ? (
                    is -= 1;
                ) : (
                    r = 0;
                );
            );
        );
        strcpy(dst, ""); // init
        strcpy(#Awst, " "); str_setchar(#Awst, 0, scl, 'cu');
        strcat(dst, ((flgs & 0x34) === 0) ? "*" : ui_jmenu_strSP);
        strcat(dst, ((flgs & 0x11) === 1) ? "!" : ui_jmenu_strSP);
        strcat(dst, #Awst);
        strcat(dst, ((flgs & 0x12) === 2) ? "#" : ui_jmenu_strSP);
        strcat(dst, ((flgs & 0x14) === 4) ? ">" : ui_jmenu_strSP);
        strcat(dst, ((flgs & 0x18) === 8) ? "<" : ui_jmenu_strSP);
        strcpy_substr(#Awst,src,cp,is-cp);
        strcat(dst, #Awst);
        idx += 1;
    );
    menu[ui_jmenu_changed] = 1;

    0 ? (
        (cItems === blockMenuITC) ? (
            sprintf(#dbg_trace, "Creating menu_Stud_Mod  %x", menu[ui_jmenu_type]);
        );
        //sprintf(#dbg_trace7, "I4:\"%s\"", menu[ui_jmenu_items+4]);
        //sprintf(#dbg_trace6, "I3:\"%s\"", menu[ui_jmenu_items+3]);
        //sprintf(#dbg_trace5, "I2:\"%s\"", menu[ui_jmenu_items+2]);
        //sprintf(#dbg_trace4, "I1:\"%s\"", menu[ui_jmenu_items+1]);
        //sprintf(#dbg_trace3, "I0:\"%s\"", menu[ui_jmenu_items+0]);
    );
    menu; // return the menu itself
);

