/*

    Standard GUI Interface

    using :
	ui-lib.jsfx-inc
	ui-lib-JJ.jsfx-inc

*/


@init


ui_lib_JJ_inited = 0;
ui_lib_JJ_GUI_size = 0;
uix_jtheme_ctlsiz = 0;
uix_jtheme = 0;
uix_jtheme_prev = -1;
uix_jtheme_menu = 0;
uix_jtheme_rtc_code = 0; // theme menu return code
uix_jtheme_rtc_max = 3;
ui_jGUI_size = 2;
ui_jshow_color = ui_jcolor_text;
ui_jd_tboxh = 0;

function ui_version(v,s,u)
(
    (v<<16)|(s<<8)|u;
);


function ui_color(rgb)
(
    ui_color((rgb >> 16) & 0xff, (rgb >> 8) & 0xff, rgb & 0xff, 1);
);


function ui_lib_JJ_updSiz()
(
    // Update some secondary values
    ui_jd_tboxh = ui_jd_tboxs = ui_jd_vtext + ui_jd_delta2; // text box size
    ui_jd_slidh = ui_jd_vtext + ui_jd_delta0; // slider height
    ui_jd_menuh = ui_jd_vtext + ui_jd_delta3; // main tabulation bar height
    ui_jd_padline = ui_jd_vtext + ui_jd_delta4; // pad line height
    ui_jd_padb2 = ui_jd_padb + ui_jd_delta2; // integated pads
    ui_jd_bboxs = ui_jd_padTBox1 * 2 + ui_jd_padTBox2 * 2; // box group border
    ui_jd_ctgr = ui_cg_lab_height + ui_cg_pad_top + ui_cg_pad_bottom + ui_spec_jd_ctgr; // add. size for the group label
    ui_jd_min_block = 2 * ui_jd_tboxs; // some arbitrary minimum
    uix_jsl_val = 0; // global slider result
    uix_jsiz_menu_bar_item_width = min(ui_jd_delta20, max(ui_jd_delta10, 0|(gfx_w / uix_jG_menuscrSz)));
    ui_cg_lab_height = uix_jtheme_ctlsiz[uix_jtheme];
);

function uix_jpadHeight(k) // height of a Pad with "k" rows
(
    ui_jd_padline * k - ui_jd_padinter + ui_jd_padb2;
);

function uix_jBoxHeight(k) // height of a show box with "k" lines
(
    ui_jd_tboxs * k + ui_jd_bboxs;
);


function ui_lib_JJ_GUI_init(size)
local (ii)
(
    // We define here some of the generic sizes
    // for the default graphic interface

    (ui_lib_JJ_inited == 0) ? (
        // The "theme" menu
        uix_jtheme_menu = 0;
        uix_jG_menuscr = make_array(32); // up to 31 main tabs
        uix_jG_menuscr[ii=0] = "Main";
        uix_jG_menuscr[ii+=1] = -1;
        uix_jtheme_ctlsiz = make_array(10); // up to 10 themes
        ui_lib_JJ_inited = 1;
    );



    (size == 2) ? (
        ui_jGUI_size = 2;
        ui_jdefault_fontsize = 15;
        ui_jdefault_fontface = #FONT;
        strcpy(ui_jdefault_fontface, "Helvetica");
        // ui_jdefault_fontface = "Arial";
        // ui_jdefault_fontface = "Verdana";
        ui_jd_vtext = 20; // text vertical size
        ui_jd_padb = 4; // pad line border
        ui_cg_pad_top = 2;
        ui_cg_pad_bottom = 1;
        ui_bx_min_w = 170;
        ui_jd_padTBox1 = 2;
        ui_jd_padTBox2 = 2;
        ui_jd_padinter = 4;
        ui_jd_padTopDec = 4;
        ui_jd_padTopDec2 = 0|(ui_jd_padTopDec * 3 / 4);
        ui_js_slLabSize = 100;
        ui_js_slValSize = 152; // 145; width of the slider text
        uix_req_h = ui_jd_vtext + 1;
        uix_javail_Wheight = 0; // avail window height - used by window displayers
        uix_javail_Bwidth = 0; // used by box displayers
        uix_javail_Bheight = 0; // avail box height - used by box displayers
        ui_jcolor_inactive_grey = 0xA9A9A9 ;
        uix_req_w = 150; // "show" min width for a text box
        uix_jtheme_prev = -1;
        uix_jG_menuscrSz = 8;
        uix_jsiz_menu_bar_item_width = 98;
        ui_lib_JJ_GUI_size = 2;
        ui_jd_scale = 1;
        ui_jd_delta0 = 0;
        ui_jd_delta1 = 2;
        ui_jd_delta2 = 3; // was 4
        ui_jd_delta3 = 8;
        ui_jd_delta4 = 11;
        ui_jd_delta10 = 52;
        ui_jd_delta20 = 90;
        uix_jtheme_ctlsiz[0] =  ui_cg_lab_height = ui_jd_vtext + 16;
        uix_jtheme_ctlsiz[1] = ui_jd_vtext + 23; // Black
        uix_jtheme_ctlsiz[2] = ui_jd_vtext + 36; // Tron
        uix_jtheme_ctlsiz[3] = ui_jd_vtext + 13;
        uix_jtheme_ctlsiz[4] = ui_jd_vtext + 13;
        uix_jtheme_ctlsiz[5] = ui_jd_vtext + 13;
        uix_jtheme_ctlsiz[6] = ui_jd_vtext + 13;
        uix_jtheme_ctlsiz[7] = ui_jd_vtext + 13;
        uix_jtheme_ctlsiz[8] = ui_jd_vtext + 13;
        uix_jtheme_ctlsiz[9] = ui_jd_vtext + 13;
    ) :




    (
        ui_jGUI_size = 1;
        ui_jdefault_fontsize = 13; // 12; // 15;
        ui_jdefault_fontface = #FONT;
        // Helvetica Baskerville Geneva Tahoma Arial
        strcpy(ui_jdefault_fontface, "Arial");
        // ui_jdefault_fontface = "Arial";
        // ui_jdefault_fontface = "Verdana";
        ui_jd_vtext = 19; // 18; // 19; // text vertical size
        ui_jd_padb = 4; // pad line border
        ui_cg_pad_top = 1;
        ui_cg_pad_bottom = 1;
        ui_bx_min_w = 132;
        ui_jd_padTBox1 = 2;
        ui_jd_padTBox2 = 2;
        ui_jd_padinter = 4;
        ui_jd_padTopDec = 4;
        ui_jd_padTopDec2 = 0|(ui_jd_padTopDec * 3 / 4);
        ui_js_slLabSize = 80;
        ui_js_slValSize = 128; // 115; width of the slider text
        uix_req_h = ui_jd_vtext + 1;
        uix_javail_Wheight = 0; // avail window height - used by window displayers
        uix_javail_Bwidth = 0; // used by box displayers
        uix_javail_Bheight = 0; // avail box height - used by box displayers
        ui_jcolor_inactive_grey = 0xA9A9A9 ;
        uix_req_w = 120; // "show" min width for a text box
        uix_jtheme_prev = -1;
        uix_jG_menuscrSz = 7;
        uix_jsiz_menu_bar_item_width = 80;
        ui_lib_JJ_GUI_size = 1;
        ui_jd_scale = 0.8;
        ui_jd_delta0 = 0;
        ui_jd_delta1 = 2;
        ui_jd_delta2 = 2; // 3; // was 4
        ui_jd_delta3 = 8;
        ui_jd_delta4 = 9;
        ui_jd_delta10 = 42;
        ui_jd_delta20 = 75;
        uix_jtheme_ctlsiz[0] =  ui_cg_lab_height = ui_jd_vtext + 12;
        uix_jtheme_ctlsiz[1] = ui_jd_vtext + 26; // Black
        uix_jtheme_ctlsiz[2] = ui_jd_vtext + 36; // Tron
        uix_jtheme_ctlsiz[3] = ui_jd_vtext + 12;
        uix_jtheme_ctlsiz[4] = ui_jd_vtext + 12;
        uix_jtheme_ctlsiz[5] = ui_jd_vtext + 12;
        uix_jtheme_ctlsiz[6] = ui_jd_vtext + 12;
        uix_jtheme_ctlsiz[7] = ui_jd_vtext + 12;
        uix_jtheme_ctlsiz[8] = ui_jd_vtext + 12;
        uix_jtheme_ctlsiz[9] = ui_jd_vtext + 12;
    );

    uix_default_fontsize = ui_jdefault_fontsize;
    uix_default_fontface = ui_jdefault_fontface;

    ui_lib_JJ_updSiz();

);


function slider_ctl(label, var, mini, maxi, slope, def, rnd)
local (r, w, k, ssv, ssx, ssvd)
(
    ui_split_top(ui_jd_slidh);
        ui_pad(5,0,5,0);
        uix_jsl_val = ssv = ssx = var;
        r = 0;
        ui_hover() ? (
            ui_click(2) ? r |= 8;
        );
        k = ui_js_slValSize;
        ui_split_left(k);
            ui_align(0, 0.5); //
            // compute the "display value"
            ssvd = ssv + ((ssv>0)?(rnd/100):(ssv<0)?(-rnd/100):0);
            ui_textnumber(ssvd, label);
            // did we clicked inside the label ?
            ui_hover() ? (
                ui_click(1) ? r |= 4;
                ui_click(2) ? r |= 8;
            );
        ui_pop();
        ui_pad(3,4,0,4); // was (3,2,0,2)
        // ssv = control_slider_x(ssv, mini, maxi, slope, def);
        ssv = uix_control_slider_h(ssv, mini, maxi, slope, 1, def, 0); //save one call level
        (r |=(ssv != ssx)) ? (
        ssvv = ssv;
            uix_jsl_val = var = ssvvr = ui_jround(ssv, rnd);
            ui_GFXdoNow |= 1;
            ui_GFXReason = 43;
        );
    ui_pop();
    r;
);

function slider_ctl_led(label, var, mini, maxi, slope, def, rnd, led1, led2)
local (r, z, w, k, ssv, ssx, ssvd)
(
    ui_split_top(ui_jd_slidh);
        ui_pad(5,0,5,0);
        uix_jsl_val = ssv = ssx = var;
        r = 0;
        ui_hover() ? (
            ui_click(2) ? r |= 8;
        );
        k = ui_js_slValSize;
        ui_split_left(k);
            ui_align(0, 0.5); //
            (led1 != 0) ? (
                ui_split_left(14);
                    ui_pad(0,3,0,3);
                    // led1[ui_jled_c_on] = gmem[RandomSColors + min(7,max(0,0|led1[ui_jled_alt]))];
                    r |= (show_jled(led1, led1[ui_jled_status], -1)) << 4 ;
                ui_pop();
            );
            // compute the "display value"
            ssvd = ssv + ((ssv>0)?(rnd/100):(ssv<0)?(-rnd/100):0);
            ui_textnumber(ssvd, label);
            // did we clicked inside the label ?
            ui_hover() ? (
                ui_click(1) ? r |= 4;
                ui_click(2) ? r |= 8;
            );
        ui_pop();
        ui_pad(0,4,0,4);  // was (0,2,0,2);
        (led2 != 0) ? (
            ui_split_left(18);
                r |= (show_jled(led2, led2[ui_jled_status], -1)) << 8;
            ui_pop();
        ) : (
            ui_pad(3,0,0,0);
        );
        // ssv = control_slider_x(ssv, mini, maxi, slope, def);
        ssv = uix_control_slider_h(ssv, mini, maxi, slope, 1, def, 0);
        (z=(ssv != ssx)) ? (
        ssvv = ssv;
            uix_jsl_val = var = ssvvr = ui_jround(ssv, rnd);
            ui_GFXdoNow |= 1;
            ui_GFXReason = 44;
        );
    ui_pop();
    r|z;
);

function ui_screen_switch(id) local(screen)
(
    uix_click_block();
    uix_key_clear();
    screen = (uix_screenstack + uix_screenstack_step*uix_screenstack_currentlayer);
    screen[0] = id;
);

function main_screen(said)
(
    said[0];
);


function uix_jmy_theme_menu() local(z)
(
    (uix_jtheme_menu != 0) ? (
        z = control_jmenu(uix_jtheme_menu);
        (z != 0) ? (
            (z <= uix_jtheme_rtc_max) ? (
                uix_jtheme = z - 1; ui_GFXdoNow |= 1; ui_GFXReason = 45;
            ) : (
                uix_jtheme_rtc_code = z; // taken into account outside
            );
        );
    );
);


function my_menu_bar(said)
local(str, str2, tw, ii, aw, ast)
(
    (gfx_w != uix_jl_gfx_w) ? (
        uix_jl_gfx_w = gfx_w;
        ui_lib_JJ_updSiz();
    );
    tw = uix_jsiz_menu_bar_item_width; // text width
    ui_split_top(ui_jd_menuh);
        uix_jmy_theme_menu();
        ui_pcolor(ui_jcolor_border);
        ui_pad(5,2); ui_border();
        ui_pcolor(ui_jcolor_text);
        aw = ui_width();

        ii = 0;
        while ((str = said[ii]) != -1) (
            (aw > tw) ? (
                aw -= tw;
                (ii == 0) ? ui_split_left(tw) : ui_split_next();
                // ui_split_left(tw);
                ui_pad(2,2,0,2);
                str2 = str;
                ((altTabsNames != 0) && ((ast = altTabsNames[ii]) != 0) && (strlen(ast) != 0)) ? (
                    str2 = ast;
                );
                (control_button(str2, str != ui_screen())) ? (
                    uix_jmbsel = ii; // selected tabulation
                    ui_screen_switch(str);
                    ui_GFXdoNow |= 1;
                    ui_GFXReason = 40;
                );
            );
            ii+= 1;
        );
        ui_pop();

        (ui_screen_level() > 0) ? (
            ui_split_right(tw);
                ui_pad(2,2);
                control_button("Back") ? (
                    ui_screen_close();
                    ui_GFXdoNow |= 1;
                    ui_GFXReason = 41;
                );
            ui_pop();
        );

        ui_pad(2,2); ui_border(); // ui_pad(1,1);

        xcol = ui_jcolor_inactive_grey;
        ((BPlc+BLpc) > 0) ? (
            AAAPxcol = xcol = displayColorsA[AAAPicol=0|(((displayColorsSiz-1)*BLpc)/(BPlc+BLpc))];
        );
        ui_show_error(xcol);

    ui_pop();

    uix_javail_Wheight = ui_height(); // New available height in the window
);


function ui_split_topratio(ratio, mini) (
    ui_split_top(max(mini,ratio*uix_drawconfig_current[19]));
);

function ui_split_leftratio(ratio,mini) (
    ui_split_left(max(mini,ratio*uix_drawconfig_current[18]));
);


function ui_split_rightratio(ratio,mini) (
    ui_split_right(max(mini,ratio*uix_drawconfig_current[18]));
);

function ui_new_column(wid)
local(wok)
(
    wok = ui_width() >= ui_bx_min_w -10;
    (wid == 0) ? ui_split_next() :
    (wid > 0) ? (
        ui_split_leftratio(wid,ui_bx_min_w);
    ) : (
        ui_split_rightratio(-wid,ui_bx_min_w);
    );
    (wok && (ui_height() > ui_jd_tboxs - 10)) ? (
        ui_pcolor(ui_jcolor_border);
        ui_pad(ui_jd_delta2, 0, ui_jd_delta2, 0); ui_border(); ui_pad(2, ui_jd_padTBox2, 2, ui_jd_padTBox2);
    ) ;
    uix_javail_Bwidth = ui_width();
    uix_javail_Bheight = ui_height();
);

/*
function ui_new_column()
(
    ui_new_column(0);
);
*/

split_leftratio = 0.35; // label width ratio

function ui_show(label, value1, value2, value3, value4, fmt)
(
    (uix_javail_Bwidth >= uix_req_w && uix_javail_Bheight >= ui_jd_tboxh) ? (
        uix_javail_Bheight -= ui_split_top(ui_jd_tboxh);
            ui_pad(0,1,0,0);
            ui_split_leftratio(split_leftratio);
                ui_pcolor(ui_jshow_color);
                ui_pad(3,0); ui_pad(0,ui_jd_padTopDec2,0,0); ui_align(0, 0.5); ui_text(label);
            ui_pop();
                ui_pcolor(ui_jcolor_border);
                ui_pad(3,0); ui_border(); ui_pad(0,ui_jd_padTopDec2,0,0); ui_align(0.5, 0.5);
                ui_pcolor(ui_jshow_color);
                value1 += (value1 > 0) ? 0.000001 : (value1 < 0) ? -0.000001 : 0;
                value2 += (value2 > 0) ? 0.000001 : (value2 < 0) ? -0.000001 : 0;
                value3 += (value3 > 0) ? 0.000001 : (value3 < 0) ? -0.000001 : 0;
                value4 += (value4 > 0) ? 0.000001 : (value4 < 0) ? -0.000001 : 0;
                ui_textnumber(value1, value2, value3, value4, fmt);
        ui_pop();
    );
    ui_jshow_color = ui_jcolor_text;
    ui_jd_tboxh = ui_jd_tboxs;
);



function ui_show(label, value1, value2, value3, fmt)
(
    (uix_javail_Bwidth >= uix_req_w && uix_javail_Bheight >= ui_jd_tboxh) ? (
        uix_javail_Bheight -= ui_split_top(ui_jd_tboxh);
            ui_pad(0,1,0,0);
            ui_split_leftratio(split_leftratio);
                ui_pcolor(ui_jshow_color);
                ui_pad(3,0); ui_pad(0,ui_jd_padTopDec2,0,0); ui_align(0, 0.5); ui_text(label);
            ui_pop();
                ui_pcolor(ui_jcolor_border);
                ui_pad(3,0); ui_border(); ui_pad(0,ui_jd_padTopDec2,0,0); ui_align(0.5, 0.5);
                ui_pcolor(ui_jshow_color);
                value1 += (value1 > 0) ? 0.000001 : (value1 < 0) ? -0.000001 : 0;
                value2 += (value2 > 0) ? 0.000001 : (value2 < 0) ? -0.000001 : 0;
                value3 += (value3 > 0) ? 0.000001 : (value3 < 0) ? -0.000001 : 0;
                ui_textnumber(value1, value2, value3, fmt);
        ui_pop();
    );
    ui_jshow_color = ui_jcolor_text;
    ui_jd_tboxh = ui_jd_tboxs;
);


function ui_show(label, value1, value2, fmt)
(
    (uix_javail_Bwidth >= uix_req_w && uix_javail_Bheight >= ui_jd_tboxh) ? (
        uix_javail_Bheight -= ui_split_top(ui_jd_tboxh);
            ui_pad(0,1,0,0);
            ui_split_leftratio(split_leftratio);
                ui_pcolor(ui_jshow_color);
                ui_pad(3,0); ui_pad(0,ui_jd_padTopDec2,0,0); ui_align(0, 0.5); ui_text(label);
            ui_pop();
                ui_pcolor(ui_jcolor_border);
                ui_pad(3,0); ui_border(); ui_pad(0,ui_jd_padTopDec2,0,0); ui_align(0.5, 0.5);
                ui_pcolor(ui_jshow_color);
                value1 += (value1 > 0) ? 0.000001 : (value1 < 0) ? -0.000001 : 0;
                value2 += (value2 > 0) ? 0.000001 : (value2 < 0) ? -0.000001 : 0;
                ui_textnumber(value1, value2, fmt);
        ui_pop();
    );
    ui_jshow_color = ui_jcolor_text;
    ui_jd_tboxh = ui_jd_tboxs;
);

function ui_show(label, value, fmt)
(
    (uix_javail_Bwidth >= uix_req_w && uix_javail_Bheight >= ui_jd_tboxh) ? (
        uix_javail_Bheight -= ui_split_top(ui_jd_tboxh);
            ui_pad(0,1,0,0);
            ui_split_leftratio(split_leftratio);
                ui_pcolor(ui_jshow_color);
                ui_pad(3,0); ui_pad(0,ui_jd_padTopDec2,0,0); ui_align(0, 0.5); ui_text(label);
            ui_pop();
                ui_pcolor(ui_jcolor_border);
                ui_pad(3,0); ui_border(); ui_pad(0,ui_jd_padTopDec2,0,0); ui_align(0.5, 0.5);
                ui_pcolor(ui_jshow_color);
                value += (value > 0) ? 0.000001 : (value < 0) ? -0.000001 : 0;
                ui_textnumber(value,fmt);
        ui_pop();
    );
    ui_jshow_color = ui_jcolor_text;
    ui_jd_tboxh = ui_jd_tboxs;
);

function ui_show(label, text)
(
    (uix_javail_Bwidth >= uix_req_w && uix_javail_Bheight >= ui_jd_tboxh) ? (
        uix_javail_Bheight -= ui_split_top(ui_jd_tboxh);
            ui_pad(0,1,0,0);
            (label != 0) ? (
                ui_pcolor(ui_jshow_color);
                ui_split_leftratio(split_leftratio);
                    ui_pad(3,0);ui_pad(0,ui_jd_padTopDec2,0,0); ui_align(0, 0.5);ui_text(label);
                ui_pop();
                    ui_pcolor(ui_jcolor_border);
                    ui_pad(3,0);
                    ui_border(); ui_pad(0,ui_jd_padTopDec2,0,0); ui_align(0.5, 0.5);
                    ui_pcolor(ui_jshow_color); ui_text(text);
            );
        ui_pop();
    );
    ui_jshow_color = ui_jcolor_text;
    ui_jd_tboxh = ui_jd_tboxs;
);

function ui_show(label)
(
    (uix_javail_Bwidth >= uix_req_w && uix_javail_Bheight >= ui_jd_tboxh) ? (
        uix_javail_Bheight -= ui_split_top(ui_jd_tboxh);
            ui_pad(0,1,0,0);
            (label != 0) ? (
                ui_pcolor(ui_jshow_color);
                ui_pad(3,0); ui_pad(0,ui_jd_padTopDec2,0,0); ui_align(0, 0.5); ui_text(label);
            );
        ui_pop();
    );
    ui_jshow_color = ui_jcolor_text;
    ui_jd_tboxh = ui_jd_tboxs;
);



function update_theme() local(main)
(
    main = main_screen(uix_jG_menuscr);
    ui_spec_jd_ctgr = 0;

    (uix_jtheme == 1) ? (
        control_start(main, "black");
    ) :
    (uix_jtheme == 2) ? (
        /*
        (ui_jGUI_size == 2) ? (
        ) : (
        );
        */
        ui_spec_jd_ctgr = 6;
        control_start(main, "tron");
    ) :
    (uix_jtheme == 3) ? (
        control_start(main, "bitmap-simple", 0);
    ) :
    (uix_jtheme == 4) ? (
        control_start(main, "bitmap-simple", 2);
    ) :
    (uix_jtheme == 5) ? (
        control_start(main, "bitmap-simple", 3);
    ) :
    (uix_jtheme == 6) ? (
        control_start(main, "bitmap-simple", 4);
    ) : (
        control_start(main, "default");
    );

    // did we change the theme ?
    uix_jtheme_prev != uix_jtheme ? (
        // if so, recompute the colors we will be using
        ui_GFXdoNow |= 1;
        ui_GFXReason = 42;
        AAAA_chg_jtheme += 1;
        uix_jtheme_prev = uix_jtheme;
        prev_control_theme = uix_control_theme;
        ui_jcolor_text = uix_theme_pcolor("text");
        ui_jcolor_draw = uix_theme_pcolor("draw");
        ui_jcolor_draw_bg = uix_theme_pcolor("bg-enabled");
        ui_jcolor_border = uix_theme_pcolor("border");
        uix_control_width_prev += 1; // make sure to reset

        ui_lib_JJ_updSiz();

    );
);


